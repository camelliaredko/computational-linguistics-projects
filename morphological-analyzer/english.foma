# Olga Redko, Feb 2, @oredko
### english.foma ###

# Vowels
define V [ a | e | i | o | u ];

read lexc english.lexc
define Lexicon;

# Consonant doubling: 1-letter consonant doubled before -ing/-ed (beg/begging)
define ConsonantDoubling g -> g g || _ "^" [i n g | e d | a b l e ] ,,
                         o d -> o d d || _ "^" [i n g | e d | a b l e ] ,,
                         p -> p p || _ "^" [i n g | e d | a b l e ] ;

# E deletion: silent e dropped before -ing, -ed, -er, -est, -able (make/making)
define EDeletion e -> 0 || _ "^" [ i n g | e d | e r | e s t | a b l e ] ;

# E insertion e added after -s, -z, -x, -ch, -sh before s (watch/watches)
define EInsertion [..] -> e || s | z | x | c h | s h _ "^" s ;

# Y replacement: -y changes to -ie before -s, -i before -ed, -i before -able (try/tries)
define YReplacement y -> i e || _ "^" s ,,
                    y -> i   || _ "^" [ e d | a b l e ] ;

# K insertion: verbs ending with vowel + -c add -k (panic/panicked)
define KInsertion [..] -> k || V c _ "^" [e d | i n g];

# LY merge: le is dropped before -ly (watchablely/watchably)
define LYMerge l e -> 0 || _ "^" [ l y ] ;

# Cleanup: remove morpheme boundaries
define Cleanup "^" -> 0;

read lexc english.lexc
define Lexicon

define Grammar Lexicon           .o.
               ConsonantDoubling .o.
               EDeletion         .o.
               EInsertion        .o.
               YReplacement      .o.
               KInsertion        .o.
               LYMerge           .o.
               Cleanup;

regex Grammar;

